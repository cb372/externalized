apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

group = "com.github.cb372"
version = "0.4.0-SNAPSHOT"

// Compile with Java 6 support
sourceCompatibility = "1.6"

repositories {
  mavenCentral()
}

dependencies {
  compile group: "org.slf4j", name: "slf4j-api", version: "1.7.5"

  testCompile group: "junit", name: "junit", version: "4.11"
  testCompile group: "org.hamcrest", name: "hamcrest-library", version: "1.3"
  testCompile group: "org.mockito", name: "mockito-core", version: "1.8.1"
  testCompile group: "commons-io", name: "commons-io", version: "2.4"
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from 'build/docs/javadoc'
}

artifacts {
  archives jar
  archives sourcesJar
  archives javadocJar
}

if (hasProperty('sonatypeUsername') && hasProperty('sonatypePassword')) {

  signing {
      sign configurations.archives
  }

  ext.isSnapshot = version.endsWith("-SNAPSHOT")
  if (isSnapshot) {
    ext.mavenRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
  } else {
    ext.mavenRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
  }

  uploadArchives {
      repositories {
          mavenDeployer {
              beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
   
              repository(url: mavenRepoUrl) {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
              }
   
              pom.project {
                 name 'externalized'
                 packaging 'jar'
                 description 'A handy DSL that makes it easier to work with external processes in Java'
                 url 'https://github.com/cb372/externalized'
   
                 scm {
                     url 'scm:git@github.com:cb372/externalized.git'
                     connection 'scm:git@github.com:cb372/externalized.git'
                     developerConnection 'scm:git@github.com:cb372/externalized.git'
                 }
   
                 licenses {
                     license {
                         name 'The Apache Software License, Version 2.0'
                         url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                         distribution 'repo'
                     }
                 }
   
                 developers {
                     developer {
                         id 'cb372'
                         name 'Chris Birchall'
                     }
                 }
             }
          }
      }
  }

}
